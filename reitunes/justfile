# The following commands ARE relevant for agents

publish:
    op run --env-file="./prod.env" -- cross build --target x86_64-unknown-linux-musl --release
    rsync ../target/x86_64-unknown-linux-musl/release/reitunes spudnik.reillywood.com:bin/
    ssh spudnik.reillywood.com -t "systemctl --user restart reitunes"

get-logs n:
    ssh -q spudnik -t "journalctl --user -u reitunes.service -n {{n}} --no-pager"

# The following commands are NOT for agent use

install-deps:
    cargo install xh --locked

watch:
    watchexec --exts=rs,sql,html,css,js --on-busy-update=restart -- cargo run -- --live-reload

run-with-secrets:
    op run --env-file="./prod.env" -- cargo run

build-with-secrets:
    op run --env-file="./prod.env" -- cargo build


run:
    cargo run

test:
    cargo nextest run

watch-tests:
    watchexec --exts=rs,sql,html -- cargo nextest run

expected_filename := if os_family() == "windows" { "reitunes-rs.exe" } else { "reitunes-rs" }

build-release:
    cargo build --release
    @$"Build size: (ls target/release/{{expected_filename}} | get size)"


build-linux-x64:
    cross build --target x86_64-unknown-linux-gnu --release

build-linux-arm64:
    cross build --target aarch64-unknown-linux-gnu --release

build-windows-on-linux:
    cross build --target x86_64-pc-windows-gnu --release
